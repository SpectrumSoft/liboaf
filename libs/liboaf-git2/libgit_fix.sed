#@author Kirill Suetnov <kyrie@yandex.ru>
# *
# * (C) SpectrumSoft, 2010-2011
# *

#Add QT_TRANSLATE_NOOP defenition after before first #include
0,/#include \(["<].*[">]\)/s//#ifndef QT_TRANSLATE_NOOP\n#define QT_TRANSLATE_NOOP(x,y) (y)\n#endif\n\n#include \1/;

#Если строка с "нашей" функцией - то переходим к её обработке, иначе пропускаем
/giterr_set/ba;
bz;

 :a
#Если в конце строки стоит кавычка (и больше ничего кроме, взможно, пробелов), то перейти к f - это объединение строк в с-стиле
/"[ \t]*$/bf
#заметить все не-кавычки от кавычки до кавычки на QT_TR_NOOP. Этот прием позволяет ограничить "жадность" regexp, заставив его взять минимальное соответствие
s/"\([^"]*\)"/QT_TRANSLATE_NOOP("libgit2", "\1")/g
#Если в конце строки ); - значит, функция закончилась, все замены в ней сделаны
/.*);[ \t]*$/bz
# ...иначе - продолжаем читать строки вызова функции. читаем следующую строку и переходим к а
n
ba

 :f
# Добавим следующую строку в конец текущей
N
# удалим лишние переносы строк
s/\n//g
#удалим вс такие кавычки, между которыми только пробелы. Собственно, делаем склеивание строк
s/"[ \t]*"//g
# и возвращаемся к вставке QT_TR_NOOP
ba

 :z



